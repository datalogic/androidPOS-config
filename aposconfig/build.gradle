plugins {
    id 'com.android.library'
    id 'maven-publish'
}
//apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.datalogic'

def libVersion = "0.2-alpha"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName libVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.room:room-runtime:2.2.6'
    implementation 'com.github.datalogic:androidpos-commons:v0.2-alpha'

    annotationProcessor 'androidx.room:room-compiler:2.2.6'

    testImplementation 'junit:junit:4.+'
    testImplementation 'com.google.truth:truth:1.1.2'
    testImplementation 'org.mockito:mockito-core:3.2.4'

    androidTestImplementation 'com.google.truth:truth:1.1.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

afterEvaluate {
    publishing {
//    def homePath = System.properties['user.home']
        publications {
            release(MavenPublication) {
                //from components.release
                groupId group
                //You can either define these here or get them from project conf elsewhere
                artifactId 'androidpos-config'
                version libVersion
                artifact("$buildDir/outputs/aar/aposconfig-release.aar")
                artifact(sourcesJar)
                //aar artifact you want to publish

                //generate pom nodes for dependencies
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                        dependencyNode.appendNode('scope', 'implementation')
                    }
                }
            }
        }

//    repositories {
//        maven {url "$homePath/AndroidRepo"}
//    }
    }
}

//artifacts {
//    archives sourcesJar
//}